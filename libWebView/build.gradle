plugins {
    id "com.android.library"
    id "org.jetbrains.kotlin.android"
}

task buildLagom(type:Exec) {
    def items = [
        "BUILD_DIR": "$buildDir".toString(),
        "CACHE_DIR": "$buildDir/caches".toString(),
        "PATH": System.getenv("PATH")
    ]
    environment items

    workingDir "../"

    commandLine "./ladybird/Ladybird/Android/BuildLagomTools.sh"
}

android {
    namespace = "io.nordcom.parrot.libwebview"

    project.tasks.preBuild.dependsOn("buildLagom") 

    compileSdk compile_sdk_version
    buildToolsVersion "$build_tool_version"

    defaultConfig {
        minSdk min_sdk_version
        compileSdk compile_sdk_version
        targetSdk compile_sdk_version

        versionCode = 1
        versionName = "0.0.1"

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags = ["-std=c++20"]
                arguments = [
                    "-DLagomTools_DIR=$buildDir/lagom-tools-install/share/LagomTools",
                    "-DSERENITY_CACHE_DIR=$buildDir/caches"
                ]
            }
        }
        ndk {
            abiFilters = ["x86_64", "arm64-v8a"]
        }
    }
    
    externalNativeBuild {
        cmake {
            path = file("../ladybird/Ladybird/CMakeLists.txt")
            version = "3.23.0+"
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
        }

        release {
            minifyEnabled = true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt")
        }
    }

    buildFeatures {
        viewBinding = true
    }

    kotlinOptions {
        jvmTarget = "20"
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_20
        targetCompatibility = JavaVersion.VERSION_20
    }
}

dependencies {
    implementation "androidx.core:core-ktx:1.10.1"
    implementation "androidx.appcompat:appcompat:1.6.1"

    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit:1.1.5"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.1"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4"
}
